#!/usr/bin/env perl
# SPDX-License-Identifier: MIT
# MIT License
#
# Copyright (c) 2020 Christoph Göttschkes
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

=encoding UTF-8
=head1 NAME

filer - Extension for URxvt which helps you open files

=head1 DESCRIPTION

This B<URxvt> extension makes it easy to open files which have been referenced in
the output of a previously executed command.
It scans through the scrollback buffer and tries to find paths to files.
It can find files referenced by absolute path, or by relative path.
To do so, it uses the current directory of the foreground process of the terminal.

By invoking the I<filer:scan> action, the scrollback buffer is scanned for
paths to files and a list is presented with the result.
This list can be navigated and the application specified by the
I<filer.launcher> resource is used to open the desired file.
Since scanning for files in the whole scrollback buffer would take a long time, if
the buffer is big, only the most recent screen is scanned.

The scanner does not only recognize paths to files, but also those, which have a
line and column as suffix in the following form:
C<E<lt>pathE<gt>:E<lt>lineE<gt>:E<lt>columnE<gt>>.
This syntax is commonly used by compilers to specify the file and the location
at which a compilation error occurred.

Since the C<E<lt>pathE<gt>:E<lt>lineE<gt>:E<lt>columnE<gt>>
syntax is not understood by every editor, the I<filer.launcher> resource
can specify the arguments to the application.
The B<perl> B<sprintf> syntax is used to do so.
The I<first> parameter is the path to the file.
The I<second> parameter is the line number.
The I<third> parameter is the column.
The parameters can be referenced using the format parameter index syntax, like C<%1$s>
for the first parameter, and C<%2$s>, C<%3$s> for the second and third respectively.
A I<filer.launcher> value of C<app --line '%2$s' --col '%3$s' '%1$s'> would
therefore expand to C<app --line '5' --col '7' 'file.txt'> if it is used to open
C<file.txt> in line C<5> and column C<7>.
It is recommended to use single quotes around the launcher arguments, especially the file
path, since it might contain whitespace or other symbols which could be
interpreted by the shell.
See the examples near the end of this document.

If the I<filer.launcher> application is a command line editor,
the program name must be prepended with a C<@> symbol to indicate this.
If this is done, the application is not executed by using B<fork> and B<exec>, but the
resulting command is printed to the tty, which makes the shell execute the
application.
This only works if the foreground process is a shell.
Because of this, the B<filer> does not work while programs like B<less>
are in the foreground, if the launcher is a command line editor.

The length of the result list is limited by the resource
I<filer.result-length>, which specifies its maximum length.

In the result list, a cursor symbol is used to indicate the currently selected
path. This symbol can be changed by using the I<filer.indicator> resource.

After scanning is completed the result list can be navigated using the B<up key>,
B<down key>, B<page-up key>, and B<page-down key>.
After the desired file has been selected, the launcher application is executed by
pressing the B<return key>.
If the desired file is not in the result list, it can be closed by pressing
the <escape key>.

=head1 INSTALLATION

B<URxvt> looks into multiple different directories when searching for extensions.
These can be configured using the B<URxvt> resource I<perl-lib>.
Please see L<urxvt(1)>.

If this extension should be installed into the users home directory, the
provided B<Makefile> can be used.
Simply run C<make install-home> as the desired user.
This installs the extension and the man page into the correct places in the users
home directory.
B<URxvt> should find the extension without the need to configure the I<perl-lib>
resource.

=head1 RESOURCES

=over 4

=item I<launcher>

Specifies the application which should be used to open the files.
Prepend a C<@> symbol, if the application is a command line editor (e.g. @nano).
The I<launcher> resource can make use of three parameters,
referenced C<%1$s>, C<%2$s>, and C<%3$s>.
The first one being the path to the file, the second the line number, and the third
the column.
Not all parameters need to be used, a value like C<@nano '%1$s'> works as well.
The parameters can be referenced in any order and each can be referenced more than once.
To bypass shell expansion of certain symbols, the I<launcher> arguments should be
in single quotes.
See the examples below.

=item I<result-length>

Configures the maximum length of the result list.

=item I<cursor>

Changes the cursor icon in the result list.

=back

=head1 EXAMPLE RESOURCES

The following shows an example resource configuration with the default values
for each resource entry:

    URxvt.filer.result-length: 5
    URxvt.filer.cursor: ►

Enable the extension and configure a keysym for the I<filer:scan> action:

    URxvt.perl-ext-common: default,filer,...
    URxvt.keysym.M-c: filer:scan

The following shows examples of launcher resources for different types of editors/IDEs.

    # %1$s = path to the file (might contain whitespaces)
    # %2$s = line number
    # %3$s = column
    ## command line editors
    URxvt.filer.launcher: @nano '+%2$s,%3$s' '%1$s'
    URxvt.filer.launcher: @ne '+%2$s,%3$s' '%1$s'
    URxvt.filer.launcher: @tilde '%1$s:%2$s:%3$s'
    URxvt.filer.launcher: @dte '+%2$s' '%1$s'
    URxvt.filer.launcher: @jed '%1$s' -g '%2$s'
    URxvt.filer.launcher: @vim '+normal %2$sG%3$s|' '%1$s'
    ## GUI editors / IDEs
    URxvt.filer.launcher: kate '%1$s:%2$s:%3$s'
    URxvt.filer.launcher: gedit '%1$s' '+%2$s:%3$s'
    URxvt.filer.launcher: atom '%1$s:%2$s:%3$s'
    URxvt.filer.launcher: gvim '+normal %2$sG%3$s|' '%1$s'
    URxvt.filer.launcher: emacsclient -n '+%2$s:%3$s' '%1$s'
    URxvt.filer.launcher: geany '--line=%2$s' '--column=%3$s' '%1$s'
    URxvt.filer.launcher: code --goto '%1$s:%2$s:%3$s'

=head1 SEE ALSO

L<urxvt(1)>, L<urxvtperl(3)>, L<xrdb(1)>

=head1 COPYRIGHT and LICENSE

This software is Copyright (c) 2020, Christoph GE<ouml>ttschkes.

This is free software; you can redistribute it and/or modify it under the terms
of the MIT license.

=cut

use warnings;
use strict;

use urxvt;

use Carp qw(croak);
use Cwd qw(realpath);
use Data::Dumper;
use Env qw(HOME EDITOR VISUAL);
use File::Basename qw(basename);
use File::Spec;
use Text::ParseWords qw(parse_line);

sub config_resource {
  my ( $self, $key, $default ) = @_;
  if ( defined( my $value = $self->x_resource("%.$key") ) ) {
    return $self->locale_decode($value);
  }
  return $default;
}

sub on_start {
  my ($self) = @_;

  $self->{launcher}  = $self->config_resource( "launcher", "" );
  $self->{indicator} = $self->config_resource( "cursor",   "►" );
  $self->{indicator_enc}     = $self->special_encode( $self->{indicator} );
  $self->{indicator_enc_len} = $self->strwidth( $self->{indicator} );
  $self->{result_length}     = $self->config_resource( "result-length", "5" );

  $self->{keysym_up}     = $self->XStringToKeysym("Up");
  $self->{keysym_down}   = $self->XStringToKeysym("Down");
  $self->{keysym_esc}    = $self->XStringToKeysym("Escape");
  $self->{keysym_return} = $self->XStringToKeysym("Return");
  $self->{keysym_pgup}   = $self->XStringToKeysym("Prior");
  $self->{keysym_pgdown} = $self->XStringToKeysym("Next");
  @{ $self->{files} } = ();

  $self->{selected} = 0;

  if ( $self->{result_length} =~ /^[1-9][0-9]*$/x ) {
    $self->{result_length} = int( $self->{result_length} );
  }
  else {
    $self->{result_length} = 5;
  }

  if ( $self->{launcher} eq "" ) {
    if ( defined($VISUAL) && $VISUAL ne "" ) {
      $self->{launcher} = $VISUAL;
    }
    elsif ( defined($EDITOR) && $EDITOR ne "" ) {
      $self->{launcher} = "@" . $EDITOR;
    }
  }

  return 1;
}

sub on_resize_all_windows {
  my ( $self, $new_width, $new_height ) = @_;
  $self->do_render(1);
  return 0;
}

sub key_pressed {
  my ( $self, $event, $keysym, $string ) = @_;
  if ( $keysym == $self->{keysym_up} ) {
    $self->{selected} =
      ( $self->{selected} == scalar @{ $self->{files} } - 1 ) ? 0 : $self->{selected} + 1;
    $self->do_render;
  }
  if ( $keysym == $self->{keysym_down} ) {
    $self->{selected} =
      ( $self->{selected} == 0 ) ? scalar @{ $self->{files} } - 1 : $self->{selected} - 1;
    $self->do_render;
  }
  if ( $keysym == $self->{keysym_pgup} ) {
    $self->{selected} = scalar @{ $self->{files} } - 1;
    $self->do_render;
  }
  if ( $keysym == $self->{keysym_pgdown} ) {
    $self->{selected} = 0;
    $self->do_render;
  }
  if ( $keysym == $self->{keysym_return} ) {
    my $launcher = $self->{launcher};
    if ( $launcher ne "" ) {
      my $file = $self->{files}[ $self->{selected} ];
      my ( $path, $line, $column, undef ) = $self->split_path( $file->{base}, $file->{path} );
      if ( $self->{launcher} =~ /%[1-3]\$s/x ) {
        # do not use sprintf, only the parameter index syntax is documented
        $launcher =~ s/%1\$s/$path/gx;
        $launcher =~ s/%2\$[sd]/$line/gx;
        $launcher =~ s/%3\$[sd]/$column/gx;
      }
      else {
        $launcher .= " '${path}'";
      }
      if ( rindex( $launcher, '@', 0 ) == 0 ) {
        $launcher = substr( $launcher, 1 );
        $self->tt_write( $self->locale_encode( $launcher . "\n" ) );
      }
      else {
        $self->exec_async( ( "/bin/sh", "-c", $launcher ) );
      }
    }
    $self->do_exit;
  }
  if ( $keysym == $self->{keysym_esc} ) {
    $self->do_exit;
  }
  return 1;
}

sub do_render {
  my ( $self, $force ) = @_;
  $force ||= 0;
  my $len = scalar @{ $self->{files} };
  my $idx = 0;
  for my $file ( @{ $self->{files} } ) {
    if ( $force || !defined( $file->{overlay} ) ) {
      if ( defined( $file->{overlay} ) ) {
        $file->{overlay}->hide;
        delete $file->{overlay};
      }
      $file->{overlay} =
        $self->overlay( 0, -$idx - 1, $self->ncol, 1, urxvt::OVERLAY_RSTYLE, 0 );
      $file->{overlay}
        ->set( $self->{indicator_enc_len} + 2, 0, $self->special_encode( $file->{path} ) );
    }
    if ( $self->{selected} == $idx ) {
      $file->{overlay}->set( 1, 0, $self->{indicator_enc} );
    }
    else {
      $file->{overlay}->set( 1, 0, $self->special_encode( " " x $self->{indicator_enc_len} ) );
    }
    $idx += 1;
  }
  return 1;
}

sub on_action {
  my ( $self, $action ) = @_;
  if ( $action eq "scan" ) {
    if ( scalar @{ $self->{files} } == 0 ) {
      $self->do_enter;
    }
    else {
      $self->do_exit;
    }
  }
  return 1;
}

sub do_enter {
  my ($self) = @_;
  $self->populate_files;
  return if ( scalar @{ $self->{files} } == 0 );
  $self->{selected} = 0;
  $self->enable( key_press => \&key_pressed );
  $self->do_render;
  return;
}

sub do_exit {
  my ($self) = @_;
  $self->disable("key_press");
  for my $file ( @{ $self->{files} } ) {
    if ( defined( $file->{overlay} ) ) {
      $file->{overlay}->hide;
      delete $file->{overlay};
    }
  }
  @{ $self->{files} } = ();
  $self->{selected} = 0;
  return;
}

sub populate_files {
  my ($self)             = @_;
  my $foreground_process = $self->proc_find_foreground_process("self");
  my $cwd                = $self->proc_cwd( $foreground_process->{pid} );
  my $row                = $self->nrow - 1;
  my $end                = -1;
  # to scan the whole scrollback buffer:
  # $end = $self->nrow - 1 - $self->total_rows;
  # might take very long, try:
  # $ ls ; head -c $(xrdb -query |grep -i 'urxvt.*saveLines' |awk '{print $2}' |xargs echo '16 *' |bc) /dev/random |hexdump -C
  while ( $row > $end && scalar @{ $self->{files} } < $self->{result_length} ) {
    my $line  = $self->line($row);
    my $files = $self->find_files( $line->t, $cwd );
    for my $file ( reverse @$files ) {
      my $base = @{$file}[0];
      my $path = @{$file}[1];
      unless ( grep { $base eq $_->{base} && $path eq $_->{path} } @{ $self->{files} } ) {
        push @{ $self->{files} },
          {
          base    => $base,
          path    => $path,
          overlay => undef,
          };
      }
      last if ( scalar @{ $self->{files} } >= $self->{result_length} );
    }
    $row = $line->beg - 1;
  }
  return;
}

sub find_files {
  my ( $self, $text, $fgp_cwd ) = @_;
  my @result = ();
  my $head   = $text;
  # split on word boundaries.
  # The outer loop moves to the right, removing things from the left.
  # The inner loop moves to the left, removing things from the right.
  # This is a greedy search, always returning the longest files, if found.
  while ( $head ne "" ) {
    my $base = $fgp_cwd;
    my $tail = $head;
    $base = "" if ( rindex( $head, "/", 0 ) == 0 || rindex( $head, "~/", 0 ) == 0 );
    if ( $self->check_path( $base, $head ) ) {
      # the whole path exists (to the end of line)
      push @result, [ $base, $head ];
      last;
    }
    while ( $tail =~ /.*[^-~\w]+/x ) {
      my $idx = $+[0];
      if ( $tail !~ /[^-~\w]$/x ) {
        # remove everything from the right, but not the matched word boundary
        $tail = substr( $tail, 0, $idx );
        if ( $self->check_path( $base, $tail ) ) {
          $head = substr( $head, $idx );
          push @result, [ $base, $tail ];
          last;
        }
      }

      # remove the word boundary found before
      $tail = substr( $tail, 0, $idx - 1 );
      if ( $tail ne "" && $self->check_path( $base, $tail ) ) {
        $head = ( length($head) > $idx - 1 ) ? substr( $head, $idx - 1 ) : "";
        push @result, [ $base, $tail ];
        last;
      }
    }
    if ( $head =~ /[^-~\w](.*)/x ) {
      $head = $1;
    }
    else {
      $head = "";
    }
  }
  return \@result;
}

sub check_path {
  my ( $self, $base, $path ) = @_;
  my ( undef, undef, undef, $ok ) = $self->split_path( $base, $path );
  return $ok;
}

sub split_path {
  my ( $self, $base, $path ) = @_;
  if ( $base ne "" ) {
    $path = File::Spec->catfile( $base, $path );
  }
  $path =~ s/^~/${HOME}/x;
  return ( $path, 0, 0, 1 ) if ( -f $path );
  # remove possible :line suffix
  if ( $path =~ /^(.*):([1-9][0-9]*)$/x ) {
    return ( $1, $2, 0, 1 ) if ( -f $1 );
  }
  # remove possible :line:column suffix
  if ( $path =~ /^(.*):([1-9][0-9]*):([1-9][0-9]*)$/x ) {
    return ( $1, $2, $3, 1 ) if ( -f $1 );
  }
  return ( $path, 0, 0, 0 );
}

sub proc_stat {
  my ( $self, $pid ) = @_;
  my $file = File::Spec->catfile( File::Spec->rootdir(), 'proc', $pid, 'stat' );
  my $stat_content;
  {
    local $/ = undef;
    open my $fh, '<', $file or croak("can't open $file: $!");
    $stat_content = <$fh>;
    close $fh;
  }

  # see man 5 proc
  my @stat_arr = $stat_content =~ /(?<=\().*(?=\))|[^\s()]+/xgs;
  return {
    pid                   => int( $stat_arr[0] ),
    comm                  => $stat_arr[1],
    state                 => $stat_arr[2],
    ppid                  => int( $stat_arr[3] ),
    pgrp                  => int( $stat_arr[4] ),
    session               => int( $stat_arr[5] ),
    tty_nr                => int( $stat_arr[6] ),
    tpgid                 => int( $stat_arr[7] ),
    flags                 => int( $stat_arr[8] ),
    minflt                => int( $stat_arr[9] ),
    cminflt               => int( $stat_arr[10] ),
    majflt                => int( $stat_arr[11] ),
    cmajflt               => int( $stat_arr[12] ),
    utime                 => int( $stat_arr[13] ),
    stime                 => int( $stat_arr[14] ),
    cutime                => int( $stat_arr[15] ),
    cstime                => int( $stat_arr[16] ),
    priority              => int( $stat_arr[17] ),
    nice                  => int( $stat_arr[18] ),
    num_threads           => int( $stat_arr[19] ),
    itrealvalue           => int( $stat_arr[20] ),
    starttime             => int( $stat_arr[21] ),
    vsize                 => int( $stat_arr[22] ),
    rss                   => int( $stat_arr[23] ),
    rsslim                => int( $stat_arr[24] ),
    startcode             => int( $stat_arr[25] ),
    endcode               => int( $stat_arr[26] ),
    startstack            => int( $stat_arr[27] ),
    kstkesp               => int( $stat_arr[28] ),
    kstkeip               => int( $stat_arr[29] ),
    signal                => int( $stat_arr[30] ),
    blocked               => int( $stat_arr[31] ),
    sigignore             => int( $stat_arr[32] ),
    sigcatch              => int( $stat_arr[33] ),
    wchan                 => int( $stat_arr[34] ),
    nswap                 => int( $stat_arr[35] ),
    cnswap                => int( $stat_arr[36] ),
    exit_signal           => int( $stat_arr[37] ),
    processor             => int( $stat_arr[38] ),
    rt_priority           => int( $stat_arr[39] ),
    policy                => int( $stat_arr[40] ),
    delayacct_blkio_ticks => int( $stat_arr[41] ),
    guest_time            => int( $stat_arr[42] ),
    cguest_time           => int( $stat_arr[43] ),
    start_data            => int( $stat_arr[44] ),
    end_data              => int( $stat_arr[45] ),
    start_brk             => int( $stat_arr[46] ),
    arg_start             => int( $stat_arr[47] ),
    arg_end               => int( $stat_arr[48] ),
    env_start             => int( $stat_arr[49] ),
    env_end               => int( $stat_arr[50] ),
    exit_code             => int( $stat_arr[51] ),
  };
}

sub proc_self_pid {
  my ($self) = @_;
  my $f = File::Spec->catdir( File::Spec->rootdir(), 'proc', 'self' );
  return int( basename( realpath($f) ) );
}

sub proc_cwd {
  my ( $self, $pid ) = @_;
  my $f = File::Spec->catdir( File::Spec->rootdir(), 'proc', $pid, 'cwd' );
  return realpath($f);
}

sub proc_children_deep {
  my ( $self, $pid, $result ) = @_;
  $result ||= [];
  my $root = File::Spec->rootdir();
  $pid = $self->proc_self_pid() if ( $pid eq "self" );
  my @childs = ();
  {
    my $childs_file = File::Spec->catfile( $root, 'proc', $pid, 'task', $pid, 'children' );
    local $/ = undef;
    open my $fh, '<', $childs_file or croak("can't open $childs_file: $!");
    my $childs_content = <$fh>;
    while ( $childs_content =~ /([0-9]+)/gx ) {
      push @childs,  int($1);
      push @$result, int($1);
    }
    close $fh;
  }
  for my $child (@childs) {
    my $grand = $self->proc_children_deep( $child, $result );
  }
  return $result;
}

sub proc_find_foreground_process {
  my ( $self, $parent ) = @_;
  my $childs = $self->proc_children_deep($parent);
  my $result = 0;
  for my $child (@$childs) {
    my $stat = $self->proc_stat($child);
    return $stat if ( $stat->{tpgid} == $stat->{pid} );
  }
  return;
}

1;
